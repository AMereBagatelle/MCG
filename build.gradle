plugins {
    id 'fabric-loom' version '1.+'
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2+"
}

archivesBaseName = project.archives_base_name + "-" + project.minecraft_version
version = project.mod_version

repositories {
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"

        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    compileOnly 'org.jetbrains:annotations:23.0.0'

    modImplementation(include(fabricApi.module("fabric-resource-loader-v0", project.fabric_version)))
    modImplementation(include(fabricApi.module("fabric-key-binding-api-v1", project.fabric_version)))

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = 'eVXHt0JE'
    versionName = "${project.archives_base_name} ${project.version}"
    versionNumber = project.version
    changelog = System.getenv('CHANGELOG_BODY')
    versionType = 'release'
    uploadFile = remapJar
    gameVersions = ["${project.minecraft_version}"]
    loaders = ['fabric']
    syncBodyFrom = rootProject.file("README.md").text
}